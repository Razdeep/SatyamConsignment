plugins {
    id 'application'
    id 'java'
    id "org.openjfx.javafxplugin" version "0.1.0"
    id 'com.diffplug.spotless' version '7.0.2'
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.controlsfx', name: 'controlsfx', version: '11.2.2'
    implementation group: 'net.sf.jasperreports', name: 'jasperreports', version: '6.1.0'
    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.34.0'
    implementation group: 'org.olap4j', name: 'olap4j', version: '1.2.0'
    implementation group: 'com.lowagie', name: 'itext', version: '2.1.7'

    implementation("org.openjfx:javafx-controls:17")
    implementation("org.openjfx:javafx-base:17")
    implementation("org.openjfx:javafx-graphics:17")
    implementation("org.openjfx:javafx-fxml:17")

    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    testCompileOnly 'org.projectlombok:lombok:1.18.20'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'

    testImplementation 'org.testfx:testfx-junit5:4.0.18'
    testImplementation 'org.testfx:openjfx-monocle:17.0.10'
    testImplementation 'org.hamcrest:hamcrest:2.2'

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.10.1"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.10.1"

    testImplementation "org.testfx:testfx-core:4.0.17"
}

javafx {
    version = "17"
    modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.graphics', 'javafx.base' ]
}

test {
    useJUnitPlatform()

    if (System.getenv("CI") == "true") {
        jvmArgs = [
                "-Dtestfx.robot=glass",
                "-Dtestfx.headless=true",
                "-Dprism.order=sw",
                "-Dprism.text=t2k",
                "-Djava.awt.headless=true"
        ]
    }
}

spotless {
    java {
        lineEndings 'WINDOWS'
        target 'src/**/*.java'
        eclipse().configFile('spotless.eclipseformat.xml')
        removeUnusedImports()
    }
}

mainClassName = 'satyamconsignment.Launcher'

application {
    mainClassName = mainClassName
}

run {
    jvmArgs = ['--add-exports=javafx.base/com.sun.javafx.event=ALL-UNNAMED']
    jvmArgs += ['--add-exports=javafx.controls/com.sun.javafx.scene.control=ALL-UNNAMED']
    jvmArgs += ['--add-exports=javafx.controls/com.sun.javafx.scene.control.behavior=ALL-UNNAMED']
    jvmArgs += ['--add-exports=javafx.base/com.sun.javafx.binding=ALL-UNNAMED']
    jvmArgs += ['--add-exports=javafx.graphics/com.sun.javafx.stage=ALL-UNNAMED']
}

shadowJar {
    archiveClassifier.set("")
}

tasks.register('copyScripts', Copy) {
    from('scripts') {
        include 'start.sh'
        include 'start.bat'
    }
    into "${layout.buildDirectory.get()}/libs"
    doLast {
        println "Copied scripts to build directory"
    }
}

tasks.named('shadowJar') {
    dependsOn (
            tasks.named('copyScripts')
    )
}
